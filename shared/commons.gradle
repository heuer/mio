/*
 * Common configuration used by all MIO subprojects.
 */

jar {
      manifest {
        name = projectName
        
        attributes 'Implementation-Version': project.version, 
                  'Implementation-Vendor': project.vendorName, 
                  'Implementation-URL': project.projectURL, 
                  'Sealed': 'false'
                  
        instruction 'Bundle-Vendor', project.vendorName
        instruction 'Bundle-License', project.licenseURL
        instruction 'Bundle-Description', project.projectDescription
        instruction 'Export-Package', '!*.internal.*, *'
      }
}

apply from: "$rootDir/shared/maven.gradle"
apply from: "$rootDir/shared/dist.gradle"

configurations {
    build {
      extendsFrom compile
    }
    all*.exclude group: 'com.thaiopensource', name: 'jing', version: '20030619'
}

repositories {
    mavenCentral()
    mavenRepo urls: 'http://www.tmapi.org/maven-repository/'
    mavenRepo urls: 'http://www.tmapi.org/maven-repository/snapshots/'
    mavenRepo urls: 'http://tinytim.sourceforge.net/maven-repository/snapshots/'
    mavenRepo urls: 'http://repository.semagia.com/thirdparty/'
    mavenRepo urls: 'http://repository.semagia.com/releases/'
    mavenRepo urls: 'http://repository.semagia.com/snapshots/'
    mavenRepo urls: 'http://repository.jboss.org/maven2/'
    mavenRepo urls: 'http://ontopia.googlecode.com/svn/maven-repository/'
    mavenRepo urls: 'http://ontopia.googlecode.com/svn/maven-snapshot-repository/'
}

dependencies {
    compile 'org.slf4j:slf4j-api:(1.5.8,)'

    testRuntime 'org.slf4j:slf4j-jdk14:(1.5.8,)',
                'org.tmapi:tmapi:(2.0.2,)',
                'org.tinytim:tinytim:2.0.1-SNAPSHOT',
                'net.ontopia:ontopia-engine:5.1.3',
                'net.ontopia:ontopia-mio:0.1.0-SNAPSHOT'
}

task copyAdditionalResources(type: Copy) {
    from "$rootDir/shared/"
    from "$projectDir"
    into sourceSets.main.classesDir
    include('LICENSE.txt')
    include('NOTICE.txt')
}

processResources.dependsOn copyAdditionalResources
