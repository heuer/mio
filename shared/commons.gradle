/*
 * Common configuration used by all MIO subprojects.
 */
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'osgi'

jar {
      manifest {
        name = project.ext.projectName
        
        attributes 'Implementation-Version': project.version, 
                   'Implementation-Vendor': project.ext.vendorName, 
                   'Implementation-URL': project.ext.projectURL, 
                   'Sealed': 'false'
                  
        instruction 'Bundle-Vendor', project.ext.vendorName
        instruction 'Bundle-License', project.ext.licenseURL
        instruction 'Bundle-Description', project.ext.projectDescription
        instruction 'Export-Package', '!*.internal.*, *'
      }
}

apply from: "$rootDir/shared/maven.gradle"
apply from: "$rootDir/shared/dist.gradle"

configurations {
    buildproject.extendsFrom(compile)
    all*.exclude group: 'com.thaiopensource', name: 'jing', version: '20030619'
}

repositories {
    mavenCentral()
    maven { url 'http://www.tmapi.org/maven-repository/' }
    maven { url 'http://www.tmapi.org/maven-repository/snapshots/' }
    maven { url 'http://tinytim.sourceforge.net/maven-repository/snapshots/' }
    maven { url 'http://repository.semagia.com/thirdparty/' }
    maven { url 'http://repository.semagia.com/releases/' }
    maven { url 'http://repository.semagia.com/snapshots/' }
    maven { url 'http://repository.jboss.org/maven2/' }
    maven { url 'http://ontopia.googlecode.com/svn/maven-repository/' }
    maven { url 'http://ontopia.googlecode.com/svn/maven-snapshot-repository/' }
}

dependencies {
    compile 'org.slf4j:slf4j-api:(1.5.8,)'

    testRuntime 'org.slf4j:slf4j-jdk14:(1.5.8,)',
                'org.tmapi:tmapi:(2.0.2,)',
                'org.tinytim:tinytim:2.0.1-SNAPSHOT',
                'net.ontopia:ontopia-engine:5.1.3',
                'net.ontopia:ontopia-mio:0.1.0-SNAPSHOT'
}

task copyAdditionalResources(type: Copy) {
    from "$rootDir/shared/"
    from "$projectDir"
    into sourceSets.main.output.classesDir
    include('LICENSE.txt')
    include('NOTICE.txt')
}

processResources.dependsOn copyAdditionalResources
